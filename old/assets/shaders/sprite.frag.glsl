#version 300 es
precision mediump float;

#ifdef DRAW_MODE_elipse
uniform float u_elipseStart;
uniform float u_elipseEnd;
uniform vec2 u_elipseRadius;
uniform vec4 u_fillColor;
uniform vec4 u_outlineColor;
uniform float u_outlineThickness;
uniform vec2 u_skinSize;
#define HALF_PI 1.570795
#define PI 3.14159
#endif
#ifdef DRAW_MODE_rectangle
uniform vec2 u_skinSize;
uniform vec4 u_fillColor;
uniform vec4 u_outlineColor;
uniform float u_outlineThickness;
#endif
#if defined(DRAW_MODE_rectangle) || defined(DRAW_MODE_elipse)
vec4 merge(vec4 color1, vec4 color2) {
  return (color1 * color1.a) + (color2 * (1.0 - color1.a));
}
#define MSAA_samplesX 3.0
#define MSAA_samplesY 3.0
#define MSAA_sampleIncrement 1.0 / (MSAA_samplesX * MSAA_samplesY)
#endif
#ifdef DRAW_MODE_silhouette
uniform vec4 u_silhouetteColor;
#else // DRAW_MODE_silhouette
# ifdef ENABLE_color
uniform float u_color;
# endif // ENABLE_color
# ifdef ENABLE_brightness
uniform float u_brightness;
# endif // ENABLE_brightness
#endif // DRAW_MODE_silhouette

#ifdef DRAW_MODE_colorMask
uniform vec3 u_colorMask;
uniform float u_colorMaskTolerance;
#endif // DRAW_MODE_colorMask

#ifdef ENABLE_fisheye
uniform float u_fisheye;
#endif // ENABLE_fisheye
#ifdef ENABLE_whirl
uniform float u_whirl;
#endif // ENABLE_whirl
#ifdef ENABLE_pixelate
uniform float u_pixelate;
uniform vec2 u_skinSize;
#endif // ENABLE_pixelate
#ifdef ENABLE_mosaic
uniform float u_mosaic;
#endif // ENABLE_mosaic
#ifdef ENABLE_ghost
uniform float u_ghost;
#endif // ENABLE_ghost
#ifdef ENABLE_red
uniform float u_red;
#endif // ENABLE_red
#ifdef ENABLE_green
uniform float u_green;
#endif // ENABLE_green
#ifdef ENABLE_blue
uniform float u_blue;
#endif // ENABLE_blue
#ifdef ENABLE_opaque
uniform float u_opaque;
#endif // ENABLE_opaque
#ifdef ENABLE_saturation
uniform float u_saturation;
#endif // ENABLE_saturation
#ifdef ENABLE_tintColor
uniform highp int u_tintColor;
#endif // ENABLE_tintColor
#if defined(ENABLE_repeatX)
uniform float u_repeatX;
#endif
#if defined(ENABLE_repeatY)
uniform float u_repeatY;
#endif
#ifdef ENABLE_tintWhites
uniform highp int u_tintWhite;
#endif
#ifdef ENABLE_tintBlacks
uniform highp int u_tintBlack;
#endif

#ifdef DRAW_MODE_line
in vec4 v_lineColor;
in float v_lineThickness;
in float v_lineLength;
#endif // DRAW_MODE_line

#ifdef DRAW_MODE_background
uniform vec4 u_backgroundColor;
#endif // DRAW_MODE_background

uniform sampler2D u_skin;

#ifndef DRAW_MODE_background
in vec2 v_texCoord;
#endif

// Add this to divisors to prevent division by 0, which results in NaNs propagating through calculations.
// Smaller values can cause problems on some mobile devices.
const float epsilon = 1e-3;

#if !defined(DRAW_MODE_silhouette) && (defined(ENABLE_color) || defined(ENABLE_saturation) || defined(ENABLE_tintColor) || defined(ENABLE_tintWhites) || defined(ENABLE_tintBlacks))
// Branchless color conversions based on code from:
// http://www.chilliant.com/rgb2hsv.html by Ian Taylor
// Based in part on work by Sam Hocevar and Emil Persson
// See also: https://en.wikipedia.org/wiki/HSL_and_HSV#Formal_derivation


// Convert an RGB color to Hue, Saturation, and Value.
// All components of input and output are expected to be in the [0,1] range.
vec3 convertRGB2HSV(vec3 rgb)
{
	// Hue calculation has 3 cases, depending on which RGB component is largest, and one of those cases involves a "mod"
	// operation. In order to avoid that "mod" we split the M==R case in two: one for G<B and one for B>G. The B>G case
	// will be calculated in the negative and fed through abs() in the hue calculation at the end.
	// See also: https://en.wikipedia.org/wiki/HSL_and_HSV#Hue_and_chroma
	const vec4 hueOffsets = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);

	// temp1.xy = sort B & G (largest first)
	// temp1.z = the hue offset we'll use if it turns out that R is the largest component (M==R)
	// temp1.w = the hue offset we'll use if it turns out that R is not the largest component (M==G or M==B)
	vec4 temp1 = rgb.b > rgb.g ? vec4(rgb.bg, hueOffsets.wz) : vec4(rgb.gb, hueOffsets.xy);

	// temp2.x = the largest component of RGB ("M" / "Max")
	// temp2.yw = the smaller components of RGB, ordered for the hue calculation (not necessarily sorted by magnitude!)
	// temp2.z = the hue offset we'll use in the hue calculation
	vec4 temp2 = rgb.r > temp1.x ? vec4(rgb.r, temp1.yzx) : vec4(temp1.xyw, rgb.r);

	// m = the smallest component of RGB ("min")
	float m = min(temp2.y, temp2.w);

	// Chroma = M - m
	float C = temp2.x - m;

	// Value = M
	float V = temp2.x;

	return vec3(
		abs(temp2.z + (temp2.w - temp2.y) / (6.0 * C + epsilon)), // Hue
		C / (temp2.x + epsilon), // Saturation
		V); // Value
}

vec3 convertHue2RGB(float hue)
{
	float r = abs(hue * 6.0 - 3.0) - 1.0;
	float g = 2.0 - abs(hue * 6.0 - 2.0);
	float b = 2.0 - abs(hue * 6.0 - 4.0);
	return clamp(vec3(r, g, b), 0.0, 1.0);
}

vec3 convertHSV2RGB(vec3 hsv)
{
	vec3 rgb = convertHue2RGB(hsv.x);
	float c = hsv.z * hsv.y;
	return rgb * c + hsv.z - c;
}

vec4 decimalToRGB(highp int decimalColor) {
	highp float blue = float(decimalColor & 0xFF) / 255.0;
	highp float green = float((decimalColor >> 8) & 0xFF) / 255.0;
	highp float red = float((decimalColor >> 16) & 0xFF) / 255.0;
	highp float alpha = float((decimalColor >> 24) & 0xFF) / 255.0;

	return vec4(red, green, blue, alpha);
}
#endif // !defined(DRAW_MODE_silhouette) && (defined(ENABLE_color) || defined(ENABLE_saturation) || defined(ENABLE_tintColor))

const vec2 kCenter = vec2(0.5, 0.5);
out vec4 outColor;

void main()
{
	#if !(defined(DRAW_MODE_line) || defined(DRAW_MODE_background))
	vec2 texcoord0 = v_texCoord;

	#ifdef ENABLE_mosaic
	texcoord0 = fract(u_mosaic * texcoord0);
	#endif // ENABLE_mosaic

	#ifdef ENABLE_pixelate
	{
		// TODO: clean up "pixel" edges
		vec2 pixelTexelSize = u_skinSize / u_pixelate;
		texcoord0 = (floor(texcoord0 * pixelTexelSize) + kCenter) / pixelTexelSize;
	}
	#endif // ENABLE_pixelate

	#ifdef ENABLE_whirl
	{
		const float kRadius = 0.5;
		vec2 offset = texcoord0 - kCenter;
		float offsetMagnitude = length(offset);
		float whirlFactor = max(1.0 - (offsetMagnitude / kRadius), 0.0);
		float whirlActual = u_whirl * whirlFactor * whirlFactor;
		float sinWhirl = sin(whirlActual);
		float cosWhirl = cos(whirlActual);
		mat2 rotationMatrix = mat2(
			cosWhirl, -sinWhirl,
			sinWhirl, cosWhirl
		);

		texcoord0 = rotationMatrix * offset + kCenter;
	}
	#endif // ENABLE_whirl

	#ifdef ENABLE_fisheye
	{
		vec2 vec = (texcoord0 - kCenter) / kCenter;
		float vecLength = length(vec);
		float r = pow(min(vecLength, 1.0), u_fisheye) * max(1.0, vecLength);
		vec2 unit = vec / vecLength;

		texcoord0 = kCenter + r * unit * kCenter;
	}
	#endif // ENABLE_fisheye

	#ifdef ENABLE_repeatX
	{
		if (u_repeatX != 1.0) texcoord0.x = fract(texcoord0.x * u_repeatX);
	}
	#endif // ENABLE_repeatX

	#ifdef ENABLE_repeatY
	{
		if (u_repeatY != 1.0) texcoord0.y = fract(texcoord0.y * u_repeatY);
	}
	#endif // ENABLE_repeatY

	#ifdef DRAW_MODE_rectangle
	vec4 color = u_fillColor;
	if (u_outlineColor.a > 0.0 && u_outlineThickness > 0.0) {
		float alpha = 0.0;
		vec2 pos = (texcoord0 * u_skinSize) - vec2(1,1);
		if (pos.x <= u_outlineThickness)
			alpha += 1.0;
		else if (pos.x >= (u_skinSize.x - u_outlineThickness))
			alpha += 1.0;
		else if (pos.y <= u_outlineThickness)
			alpha += 1.0;
		else if (pos.y >= (u_skinSize.y - u_outlineThickness))
			alpha += 1.0;
		color = merge(vec4(u_outlineColor.rgb, min(alpha * u_outlineColor.a, 1.0)), color);
	}
	outColor = color;
	#endif
	#ifdef DRAW_MODE_elipse
	vec4 color = u_fillColor;
	float alpha;
	if (u_fillColor.a > 0.0) {
		alpha = 0.0;
		vec2 dif = texcoord0 - vec2(0.5, 0.5);
		float dist = sqrt((dif.x * dif.x) + (dif.y * dif.y));
		float dir = atan(dif.y, dif.x) + PI;
		if (dist <= 0.5 && dir >= u_elipseStart && dir <= u_elipseEnd)
			alpha += 1.0;
		color = vec4(u_fillColor.rgb, min(alpha * u_fillColor.a, 1.0));
	}
	if (u_outlineThickness > 0.0 && u_outlineColor.a > 0.0) {
		alpha = 0.0;
		vec2 dif = texcoord0 - vec2(0.5, 0.5);
		float dist = sqrt((dif.x * dif.x) + (dif.y * dif.y));
		float dir = atan(dif.y, dif.x) + PI;
		float dire = abs((HALF_PI - abs(atan(dif.y, dif.x))) / HALF_PI);
		if (dist <= 0.5 && dist >= (0.5 - (u_outlineThickness / ((u_skinSize.x * dire) + (u_skinSize.y * (1.0- dire))))) && dir >= u_elipseStart && dir <= u_elipseEnd)
			alpha += 1.0;
		color = merge(vec4(u_outlineColor.rgb, min(alpha * u_outlineColor.a, 1.0)), color);
	}
	outColor = color;
	#endif
	#ifdef DRAW_MODE_default
	outColor = texture2D(u_skin, texcoord0);
	#endif

	#if defined(ENABLE_color) || defined(ENABLE_brightness) || defined(ENABLE_saturation) || defined(ENABLE_tintColor)
	// Divide premultiplied alpha values for proper color processing
	// Add epsilon to avoid dividing by 0 for fully transparent pixels
	outColor.rgb = clamp(outColor.rgb / (outColor.a + epsilon), 0.0, 1.0);

	#ifdef ENABLE_color
	{
		vec3 hsv = convertRGB2HSV(outColor.xyz);

		// this code forces grayscale values to be slightly saturated
		// so that some slight change of hue will be visible
		
		// pm: this usually ends up looking ugly in menus and such, so dont do this actually
		// 	   this might be reverted to do this again though if it is genuinely better

		// const float minLightness = 0.11 / 2.0;
		// const float minSaturation = 0.09;
		// if (hsv.z < minLightness) hsv = vec3(0.0, 1.0, minLightness);
		// else if (hsv.y < minSaturation) hsv = vec3(0.0, minSaturation, hsv.z);

		hsv.x = mod(hsv.x + u_color, 1.0);
		if (hsv.x < 0.0) hsv.x += 1.0;

		outColor.rgb = convertHSV2RGB(hsv);
	}
	#endif // ENABLE_color
	
	#ifdef ENABLE_saturation
	{
		vec3 hsv = convertRGB2HSV(outColor.xyz);

		hsv.y *= u_saturation;

		outColor.rgb = convertHSV2RGB(hsv);
	}
	#endif // ENABLE_saturation
	
	#ifdef ENABLE_tintColor
	{
		vec4 tintRgb = decimalToRGB(u_tintColor);

		outColor *= tintRgb;
	}
	#endif // ENABLE_tintColor

	#ifdef ENABLE_brightness
	outColor.rgb = clamp(outColor.rgb + vec3(u_brightness), vec3(0), vec3(1));
	#endif // ENABLE_brightness

	// Re-multiply color values
	outColor.rgb *= outColor.a + epsilon;

	#endif // defined(ENABLE_color) || defined(ENABLE_brightness) || defined(ENABLE_saturation) || defined(ENABLE_tintColor)

	#ifdef ENABLE_ghost
	outColor *= u_ghost;
	#endif // ENABLE_ghost
	
	#ifdef ENABLE_red
	outColor.r *= u_red;
	#endif // ENABLE_red
	
	#ifdef ENABLE_green
	outColor.g *= u_green;
	#endif // ENABLE_green
	
	#ifdef ENABLE_blue
	outColor.b *= u_blue;
	#endif // ENABLE_blue
	
	#ifdef ENABLE_opaque
	outColor.a *= u_opaque;
	#endif // ENABLE_opaque

	#if defined(ENABLE_tintBlacks) || defined(ENABLE_tintWhites)
	{
		vec4 color = outColor;
		#ifdef ENABLE_tintWhites
		{
			vec4 tintRgb = decimalToRGB(u_tintWhite);
			color = tintRgb * outColor;
		}
		#endif

		#ifdef ENABLE_tintBlacks
		{
			vec4 tintRgb = decimalToRGB(u_tintBlack);
			color = ((vec4(1,1,1,1) - outColor) * tintRgb) + color;
		}
		#endif
		outColor = color;
	}
	#endif

	#ifdef DRAW_MODE_silhouette
	// Discard fully transparent pixels for stencil test
	if (outColor.a == 0.0) {
		discard;
	}
	// switch to u_silhouetteColor only AFTER the alpha test
	outColor = u_silhouetteColor;
	#else // DRAW_MODE_silhouette

	#ifdef DRAW_MODE_colorMask
	vec3 maskDistance = abs(outColor.rgb - u_colorMask);
	vec3 colorMaskTolerance = vec3(u_colorMaskTolerance, u_colorMaskTolerance, u_colorMaskTolerance);
	if (any(greaterThan(maskDistance, colorMaskTolerance)))
	{
		discard;
	}
	#endif // DRAW_MODE_colorMask
	#endif // DRAW_MODE_silhouette

	#ifdef DRAW_MODE_straightAlpha
	// Un-premultiply alpha.
	outColor.rgb /= outColor.a + epsilon;
	#endif

	#endif // !(defined(DRAW_MODE_line) || defined(DRAW_MODE_background))

	#ifdef DRAW_MODE_line
	// Maaaaagic antialiased-line-with-round-caps shader.

	// "along-the-lineness". This increases parallel to the line.
	// It goes from negative before the start point, to 0.5 through the start to the end, then ramps up again
	// past the end point.
	float d = ((v_texCoord.x - clamp(v_texCoord.x, 0.0, v_lineLength)) * 0.5) + 0.5;

	// Distance from (0.5, 0.5) to (d, the perpendicular coordinate). When we're in the middle of the line,
	// d will be 0.5, so the distance will be 0 at points close to the line and will grow at points further from it.
	// For the "caps", d will ramp down/up, giving us rounding.
	// See https://www.youtube.com/watch?v=PMltMdi1Wzg for a rough outline of the technique used to round the lines.
	float line = distance(vec2(0.5), vec2(d, v_texCoord.y)) * 2.0;
	// Expand out the line by its thickness.
	line -= ((v_lineThickness - 1.0) * 0.5);
	// Because "distance to the center of the line" decreases the closer we get to the line, but we want more opacity
	// the closer we are to the line, invert it.
	outColor = v_lineColor * clamp(1.0 - line, 0.0, 1.0);
	#endif // DRAW_MODE_line

	#ifdef DRAW_MODE_background
	outColor = u_backgroundColor;
	#endif

	// premult hint doesnt seem to do anything, so just always statically premultiply
	outColor = vec4(outColor.rgb * outColor.a, outColor.a);
}
